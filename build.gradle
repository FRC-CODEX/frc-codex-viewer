plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.5'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.frc.codex'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:3.5.5"
	developmentOnly 'org.springframework.boot:spring-boot-devtools:3.5.5'
	implementation 'com.google.guava:guava:33.4.8-jre'
	implementation 'com.opencsv:opencsv:5.12.0'
	implementation 'com.zaxxer:HikariCP:7.0.2'
	implementation 'org.flywaydb:flyway-core:11.11.2'
	implementation 'org.flywaydb:flyway-database-postgresql:11.11.2'
	implementation 'org.postgresql:postgresql:42.7.7'
    implementation 'org.springframework.boot:spring-boot-starter-security:3.5.5'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf:3.5.5'
	implementation 'org.springframework.boot:spring-boot-starter-web:3.5.5'
	implementation 'software.amazon.awssdk:cloudwatch:2.33.0'
	implementation 'software.amazon.awssdk:lambda:2.33.0'
	implementation 'software.amazon.awssdk:netty-nio-client:2.33.0'
	implementation 'software.amazon.awssdk:s3:2.33.0'
	implementation 'software.amazon.awssdk:sqs:2.33.0'
	testImplementation 'org.springframework.boot:spring-boot-starter-test:3.5.5'
}

tasks.named('test') {
	useJUnitPlatform()
	environment "AWS_REGION", "eu-west-2"
}

tasks.register('copyNodeModules', Copy) {
	description = 'Copy node_modules assets.'
	into "src/main/resources/static"
	into("/") {
		from "node_modules/govuk-frontend/dist/govuk/govuk-frontend.min.css"
		from "node_modules/govuk-frontend/dist/govuk/govuk-frontend.min.js"

		from "node_modules/i18next-browser-languagedetector/i18nextBrowserLanguageDetector.min.js"
		from "node_modules/i18next/i18next.min.js"

		from "node_modules/ixbrl-viewer/iXBRLViewerPlugin/viewer/dist/ixbrlviewer.js"
	}
	into("assets") {
		from "node_modules/govuk-frontend/dist/govuk/assets/manifest.json"
	}
	into("assets/fonts") {
		from "node_modules/govuk-frontend/dist/govuk/assets/fonts"
	}
	into("assets/images") {
		from "node_modules/govuk-frontend/dist/govuk/assets/images"
	}

}

tasks.named('processResources') {
	dependsOn 'copyNodeModules'
}
